apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

// Check dependencies.
// https://github.com/ben-manes/gradle-versions-plugin
apply plugin: 'com.github.ben-manes.versions'

// Detekt - Static code analysis
// https://github.com/arturbosch/detekt
apply from: "../config/quality/detekt/detekt.gradle"

// Fabric
apply plugin: 'io.fabric'

// Signing production apk with rootProject.ext.releaseFileSpaceSigningConfig data
if (file("../config/signing/filespace-signing.gradle").exists()) {
    apply from: '../config/signing/filespace-signing.gradle'
} else {
    apply from: '../config/signing/filespace-signing-template.gradle'
}

// Lint, pmd...
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.mercandalli.android.apps.files"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.appVersionCode
        versionName rootProject.ext.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "ar", "es", "fr", "en", "pt-rBR", "ru", "zh-rTW"

        // https://github.com/Dimezis/BlurView
        renderscriptTargetApi rootProject.ext.targetSdkVersion
        renderscriptSupportModeEnabled true
    }

    bundle {
        density.enableSplit = true
        abi.enableSplit = true
        language.enableSplit = false
    }

    signingConfigs {
        releaseSigningConfig rootProject.ext.releaseFileSpaceSigningConfig
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releaseSigningConfig
            minifyEnabled true
            //shrinkResources true
            multiDexEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix ".dev"
            shrinkResources false
        }
    }

    sourceSets {
        main {
            // Split resources.
            // https://medium.com/google-developer-experts/android-project-structure-alternative-way-29ce766682f0#.sjnhetuhb
            res.srcDirs = [
                    'src/main/res/android_q',
                    'src/main/res/bottom_bar',
                    'src/main/res/common',
                    'src/main/res/dialog',
                    'src/main/res/file_column_detail',
                    'src/main/res/file_column_horizontal_lists',
                    'src/main/res/file_column_list',
                    'src/main/res/file_column_row',
                    'src/main/res/file_details',
                    'src/main/res/file_list',
                    'src/main/res/file_list_row',
                    'src/main/res/file_online',
                    'src/main/res/file_online_upload',
                    'src/main/res/icon',
                    'src/main/res/main',
                    'src/main/res/note',
                    'src/main/res/notification',
                    'src/main/res/permission',
                    'src/main/res/search',
                    'src/main/res/settings',
                    'src/main/res/settings_android_q',
                    'src/main/res/settings_full_version',
                    'src/main/res/settings_ram',
                    'src/main/res/settings_storage',
                    'src/main/res/settings_store'
            ]
        }

        // Everything related to connected test live in src/androidTest/core/java
        androidTest.java.srcDir 'src/androidTest/core/java'

        // Instrumented unit tests live in src/androidTest/unit
        androidTest.java.srcDir 'src/androidTest/unit/java'

        // Instrumented integration tests live in src/androidTest/integration
        androidTest.java.srcDir 'src/androidTest/integration/java'

        // Instrumented use cases tests live in src/androidTest/usecases
        androidTest.java.srcDir 'src/androidTest/usecases/java'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dynamicFeatures = [":app_search_dynamic"]
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

configurations {
    all {
        exclude group: 'org.json', module: 'json'
    }
    // KtLint - Static code analysis
    // https://proandroiddev.com/kotlin-static-analysis-why-and-how-a12042e34a98
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':feature_purchase')
    api project(':file_api_android')
    api project(':file_api_online_android')

    // Language
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Android - Support
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    api "androidx.recyclerview:recyclerview:1.0.0"

    // Google - Firebase
    implementation "com.google.firebase:firebase-core:16.0.8"
    implementation "com.google.firebase:firebase-config:16.5.0"

    // Google dynamic delivery feature
    api "com.google.android.play:core:1.4.1"

    // Third party - Monitoring
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    // Third party - Network
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp:okhttp-urlconnection:2.7.5"

    // Third party - UI
    api 'com.hannesdorfmann:adapterdelegates4:4.0.0'
    // https://github.com/Dimezis/BlurView
    implementation 'com.eightbitlab:blurview:1.5.0'

    // KtLint - Static code analysis
    // https://proandroiddev.com/kotlin-static-analysis-why-and-how-a12042e34a98
    ktlint "com.github.shyiko:ktlint:0.31.0"

    // Testing-only dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.26.0'

    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

// KtLint - Static code analysis
// https://proandroiddev.com/kotlin-static-analysis-why-and-how-a12042e34a98
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // args "--reporter=checkstyle, output=${buildDir}/ktlint.xml
}

apply plugin: 'com.google.gms.google-services'
