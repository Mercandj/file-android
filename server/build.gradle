group 'file-server'

buildscript {
    ext.ktor_version = '1.1.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'com.mercandalli.server.files.main.MainKt'

sourceCompatibility = 1.8
compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

sourceSets {
    main.kotlin.srcDirs += 'main/kotlin'
    test.kotlin.srcDirs += 'test/kotlin'
    test.resources.srcDirs = [ 'test/resources' ]
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}

configurations {
    // KtLint - Static code analysis
    // https://proandroiddev.com/kotlin-static-analysis-why-and-how-a12042e34a98
    ktlint
}

dependencies {
    //implementation project(path: ':file_api', configuration: 'default')
    compile project(path: ':file_api_online', configuration: 'default')
    compile project(":feature_event_mercan")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-auth:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"

    compile "ch.qos.logback:logback-classic:1.2.1"
    compile 'org.json:json:20180813'

    // KtLint - Static code analysis
    // https://proandroiddev.com/kotlin-static-analysis-why-and-how-a12042e34a98
    ktlint "com.github.shyiko:ktlint:0.31.0"

    // Testing-only dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.23.4'
    testCompile project(':file_api').sourceSets.test.output
    // testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    baseName "file-server"
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'com.mercandalli.server.files.main.MainKt'
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.mercandalli.server.files.main.MainKt'
    }
    baseName = "file-server"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    destinationDir = file("$rootDir/build/")
}

// KtLint - Static code analysis
// https://proandroiddev.com/kotlin-static-analysis-why-and-how-a12042e34a98
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // args "--reporter=checkstyle, output=${buildDir}/ktlint.xml
}